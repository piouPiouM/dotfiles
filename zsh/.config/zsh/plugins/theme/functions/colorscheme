# -------------------------------------------------------------------
# vim:et sts=2 sw=2 ft=zsh
# -------------------------------------------------------------------
# @name colorscheme
# @description Apply the colorscheme corresponding to the given mode to my tools.
# @arg $1 string One of available modes.

typeset -Ar _mode_map=(
  "dark" "catppuccin-mocha"
  "light" "catppuccin-latte"
  "soft-dark" "rose-pine"
  "soft-light" "rose-pine-dawn"
)

_success() {
  echo "ï€Œ Theme applied for ${1}"
}

local -r _mode=$1

if [[ -z $_mode_map[$_mode] ]]; then
  echo "Unknown mode given. Use one of ${(k)_mode_map}."
  return 1
fi

local -r _theme=$_mode_map[$_mode]

# Use `>|` to bypass noclobber option
echo $_mode >| "${HOME}/.theme" 
theme-kitty $_theme && _success "Kitty"
theme-bat $_theme && _success "Bat"
theme-fzf $_theme && _success "fzf"
theme-lazygit $_theme && _success "Lazygit"
zimfw compile
